package Analizadores;
import java_cup.runtime.*;
import java_cup.runtime.Symbol;
import Tabla.*;
import java.util.ArrayList;
import java.util.LinkedList;
import java.util.Queue;
import java.util.Stack;
import Polaca.*;
// ---------> Codigo para las acciones gramaticales
action code
{:
     public TablaDeSimbolos tablaDeSimbolos = new TablaDeSimbolos();
     public Queue<String> colaNombres = new LinkedList();
     public Queue<String> colaTipoDato = new LinkedList(); 
     public Stack<Integer> pila = new Stack<Integer>();
     public ArrayList<String> polaca = new ArrayList<String>();
     public String comparadorString = "";
     public int hayDobleComparacion= 0;
     public int isOr = 0;
     public Stack<Integer> posicionesBloqueVerdaderoOr = new Stack<Integer>();
     public int contador = 0;
     public int constanteAContar = 0;  
     public Polaca polacaManager = new Polaca();
     public void insertarEnPolaca(String contenido){
          polaca.add(contenido);
     }

     public String obtenerContrario(int posi){
          String original = polaca.get(posi);
          String contrario = ""; 
          switch(original){
               case "BLT": 
                    contrario= "BGE";
               break;
               case "BGE":
                    contrario= "BLT";
               break;
               case "BLE":
                    contrario= "BGT";
               break;
               case "BGT":
                    contrario= "BLE";
               break;
               case "BEQ":
                    contrario= "BNE";
               break;
               case "BNE":
                    contrario= "BEQ";
               break;
               default:
                    contrario= " ERROR ";
               break;

          }
          return contrario; 
     }

     public void insertarEnPolaca(String contenido, Integer lugar){
          polaca.set(lugar , contenido);
     }

     public void avanzar() {
          polaca.add(" ");
     }

:}


parser code     //Codigo que se le agrega al parser
{:
    public void syntax_error(Symbol cur_token) {
        String errMsg = "Syntax error " + cur_token.value.toString() + " " + cur_token.right + ":" + cur_token.left + "\n";
        report_error(errMsg, null);
    }

    public void unrecovered_syntax_error(Symbol cur_token) throws Exception {
        String errMsg = "Couldn't repair and continue parse " + cur_token.value.toString() + " " + cur_token.right + ":" + cur_token.left + "\n";
        report_fatal_error(errMsg, null);
    }
:}

//Declaracion de terminales
terminal  String PUT, GET, DIM, AS, INTEGER, STRING, FLOAT, WHILE, IF, ELSE, 
          OP_AS, OP_SUM, OP_RES, OP_MUL, OP_DIV, OP_MEN, OP_MENIG, OP_MAY, OP_MAYIG, OP_DIST, 
          COMA, PYC, PA, PC, LA, LC, CA, CC, OP_AND, OP_OR, 
          OP_COMP, OP_NOT, CONTAR;


terminal String ID, CONST_INT, CONST_FLOAT, CONST_STR, CTE_HEXA, CTE_BIN;


//Declaracion de no terminales
non terminal Symbol inicio, prog, asig, sent, vacio, 
                    expresion, termino, factor, seleccion,
                    iteracion, condicion,comparacion, comparador, 
                    seleccion_else, dec, sent_put, sent_get,
                    list_var, list_td, tipo_dato, variable, list_constantes, constantes;

start with inicio;
//Reglas gramaticales
inicio          ::= prog
                    {:  tablaDeSimbolos.guardarTabla();
                         System.out.println("R1: inicio -> prog");
                         System.out.println("COMPILACION EXITOSA!");
                         polacaManager.guardarPolaca(polaca); 
                    :};  

prog           ::= sent
                    {: 
                         System.out.println("R2: prog -> sent");
                    :}
               | prog sent
                    {:
                         System.out.println("R3: prog -> prog sent");
                    :}; 
sent                ::= asig PYC
                    {: 
                         System.out.println("R4: sent -> asig");
                    :}
                    | iteracion 
                    {:
                         System.out.println("R5: sent -> iteracion");
                    :}
                    | dec
                     {: 
                         System.out.println("R6: sent -> dec");
                    :}
                    | sent_put PYC
                     {: 
                         System.out.println("R7: sent -> sent_put PYC");
                    :}
                    | sent_get PYC
                     {: 
                         System.out.println("R8: sent -> sent_get PYC");
                    :}
                    | seleccion
                    {: 
                      System.out.println("R9: sent -> seleccion");
                    :}; 

asig           ::= ID:ID OP_AS:OP_AS expresion
                    {: 
                         insertarEnPolaca(ID);
                         insertarEnPolaca(OP_AS);
                         System.out.println("R10: asig -> ID OP_ASig expresion");
                    :};

iteracion      ::= WHILE {:
                         pila.push(polaca.size());
                         insertarEnPolaca("ET");
                         RESULT = new Symbol(-1); 
                    :}PA condicion {:
                         if(isOr != 0){
                           posicionesBloqueVerdaderoOr.push(polaca.size());
                         }
                         RESULT = new Symbol(-1); 
                    :}PC LA prog {:
                         insertarEnPolaca("BI");
                         if(hayDobleComparacion != 0){
                             insertarEnPolaca(String.valueOf(polaca.size() + 1), pila.pop() );
                              if(isOr != 0){
                                   insertarEnPolaca(String.valueOf(posicionesBloqueVerdaderoOr.pop()), pila.pop());
                                   isOr--; 
                              } else {
                                   insertarEnPolaca(String.valueOf(polaca.size() + 1), pila.pop() );
                              }
                              hayDobleComparacion--; 
                         } else {
                         insertarEnPolaca(String.valueOf(polaca.size() + 1), pila.pop() );
                         }

                        insertarEnPolaca(String.valueOf(pila.pop()));

                         RESULT = new Symbol(-1); 
                    :}  LC
                    {: 
                         System.out.println("R11: iteracion -> WHILE condicion  LA prog  lc");
                    :}; 
seleccion      ::= IF  PA condicion PC LA{:
                    if(isOr!=0){
                         posicionesBloqueVerdaderoOr.push(polaca.size());
                    }
                    RESULT = new Symbol(-1); 
                    :} prog LC seleccion_else; 


seleccion_else ::= ELSE {:
                         insertarEnPolaca("BI");
                          insertarEnPolaca(String.valueOf(polaca.size()+1), pila.pop() );
                          if(hayDobleComparacion != 0){
                               if(isOr!=0){
                                    insertarEnPolaca(String.valueOf(posicionesBloqueVerdaderoOr.pop()), pila.pop() );
                                   isOr--;
                               }else {
                                    insertarEnPolaca(String.valueOf(polaca.size()+1), pila.pop() );
                               }
                               hayDobleComparacion--; 
                          }
                         pila.push(polaca.size() );
                         avanzar();
                         RESULT = new Symbol(-1); 
               :} LA prog LC
                {:
                         insertarEnPolaca(String.valueOf(polaca.size()), pila.pop() );
                         System.out.println("R14: seleccion_else -> IF condicion LA prog LC ELSE prog");

                :} 
                | 
                vacio 
                {: 
                     if(hayDobleComparacion != 0){
                          insertarEnPolaca(String.valueOf(polaca.size()), pila.pop() );
                          if(isOr != 0){
                              insertarEnPolaca(String.valueOf(posicionesBloqueVerdaderoOr.pop()), pila.pop() );
                               isOr--;
                          } else {
                              insertarEnPolaca(String.valueOf(polaca.size()), pila.pop() );
                          }
                    
                         hayDobleComparacion--; 
                     } else {
                          insertarEnPolaca(String.valueOf(polaca.size()), pila.pop() );
                     }
                     System.out.println("If sin else"); 
                     RESULT = new Symbol(-1); 
                :};
                
vacio          ::= {:
                    RESULT = new Symbol(-1);
               :};
               

condicion      ::= comparacion
                    {: 
                         System.out.println("R15: condicion -> comparacion");
                    :}
                    | comparacion OP_AND  comparacion
                    {: 
                         hayDobleComparacion++; 
                         System.out.println("R16: condicion -> comparacion OP_AND  comparacion");
                    :} 
                    | comparacion OP_OR {: 
                         String contrario = obtenerContrario(polaca.size()-2);
                         isOr++;   
                         insertarEnPolaca(contrario, polaca.size() - 2);
                         RESULT = new Symbol(-1); 

                    :} comparacion
                    {: 
                         hayDobleComparacion++; 
                         System.out.println("R17: condicion -> comparacion OP_OR comparacion");
                    :}
                    | OP_NOT comparacion
                    {: 
                         String contrario = obtenerContrario(polaca.size()-2);
                         insertarEnPolaca(contrario, polaca.size() - 2);
                          System.out.println("R18: condicion -> OP_NOT comparacion");
                    :}; 
comparacion    ::= expresion comparador expresion
                    {: 
                        insertarEnPolaca("CMP");
                         insertarEnPolaca(comparadorString);
                         pila.push(polaca.size());
                         avanzar();
                          System.out.println("R19: comparacion -> expresion comparador expresion");
                    :}; 

comparador     ::= OP_MAYIG 
                    {:  
                         comparadorString = "BLT"; 
                         System.out.println("comparador -> OP_MAYIG " );

		          :}
                    | OP_MENIG 
                    {: 
                         comparadorString = "BGT"; 
                         System.out.println("comparador -> OP_MENIG" );
		          :}
                    | OP_MAY 
                    {: 
                         comparadorString = "BLE"; 
                         System.out.println("comparador -> OP_MAY" );
		          :}
                    | OP_MEN
                    {: 
                         comparadorString = "BGE"; 
                         System.out.println("comparador -> OP_MEN");
		          :}
                    | OP_COMP
                    {: 
                         comparadorString = "BNE"; 
                         System.out.println("comparador -> OP_COMP" );
                      
		          :}
                    | OP_DIST
                    {: 
                         comparadorString = "BEQ"; 
                         System.out.println("comparador -> OP_DIST" );
		          :};
        
expresion      ::= expresion OP_SUM:OP_SUM termino
               {:
                    System.out.println("expresion -> expresion OP_SUM termino");
                    insertarEnPolaca(OP_SUM);    
               :}
               | expresion OP_RES:OP_RES termino
               {:
                    System.out.println("expresion -> expresion OP_RES termino");  
                    insertarEnPolaca(OP_RES);   
               :}
               | termino
               {:
                    System.out.println("expresion -> termino");

               :};

termino        ::=  termino OP_DIV:OP_DIV factor
               {: 
                    System.out.println("termino -> termino OP_DIV factor");
                    insertarEnPolaca(OP_DIV);
		     :}
		     | termino OP_MUL:OP_MUL factor 
               {: 
                    System.out.println("termino -> termino OP_MUL factor");
                    insertarEnPolaca(OP_MUL);
		     :}
               | factor 
               {: 
                    System.out.println("termino -> factor");
               :};
               
variable       ::= CONST_STR:CONST_STR {:
                    String stringSinComillas = CONST_STR.substring(0, CONST_STR.length() - 1);
                    tablaDeSimbolos.agregarEnTabla("_"+stringSinComillas, null, stringSinComillas, stringSinComillas.length());
                    System.out.println("variable -> CONST_STRING");
                    insertarEnPolaca(CONST_STR);
               :} 
               | ID:ID {:
                         System.out.println("variable -> ID");
                         insertarEnPolaca(ID);
               :}
               | CONST_INT:CONST_INT {: 
                         tablaDeSimbolos.agregarEnTabla("_"+CONST_INT, null, CONST_INT, null);
                         System.out.println("variable -> CONST_INT");
                         insertarEnPolaca(CONST_INT);
               :}
               | CONST_FLOAT:CONST_FLOAT {: 
                         tablaDeSimbolos.agregarEnTabla("_"+CONST_FLOAT, null, CONST_FLOAT, null);
                         System.out.println("variable -> CONST_FLOAT");
                        insertarEnPolaca(CONST_FLOAT);
               :}
                | CTE_BIN:CTE_BIN {: 
                         String valorBin = CTE_BIN.substring(2);
                         String valorDecimal = String.valueOf(Integer.parseInt(valorBin,2));
                        insertarEnPolaca(valorDecimal);
                         tablaDeSimbolos.agregarEnTabla("_"+CTE_BIN, null, valorBin, null);
                         System.out.println("variable -> CTE_BIN");
               :}
                | CTE_HEXA:CTE_HEXA {:
                         String valorHexa = CTE_HEXA.substring(2);
                         String valorDecimal = String.valueOf(Integer.parseInt(valorHexa,16));
                         insertarEnPolaca(valorDecimal);
                         tablaDeSimbolos.agregarEnTabla("_"+CTE_HEXA, null, valorDecimal, null);
                         System.out.println("variable -> CTE_HEXA");
               :};
sent_put       ::= PUT:PUT variable {:
                    insertarEnPolaca(PUT);
                    System.out.println("sent_put -> PUT variable");
               :};

sent_get     ::= GET:GET ID:ID {:
                    insertarEnPolaca(ID);
                    insertarEnPolaca(GET);
                    System.out.println("sent_get -> GET ID");
               :};

dec           ::= DIM OP_MEN list_var OP_MAY AS OP_MEN list_td OP_MAY {:
                    tablaDeSimbolos.agregarVariables(colaNombres, colaTipoDato);
                    System.out.println("dec -> DIM OP_MEN list_var OP_MAY AS OP_MEN list_td OP_MAY");
               :};    


list_var ::= list_var COMA ID:ID
               {:
                         colaNombres.add(ID);
                         System.out.println("list_var -> list_var COMA ID: ");
               :};

list_var ::= ID:ID
               {:
                    colaNombres.add(ID);
                     tablaDeSimbolos.agregarEnTabla("_"+ID, null, ID, null);
                     System.out.println("list_var -> ID ");
               :};
               
list_td ::= list_td COMA tipo_dato
               {:
                         System.out.println("list_td -> list_td COMA tipo_dato");
               :};
               
list_td ::= tipo_dato
               {:
                         System.out.println("list_td -> TIPO DE DATO ");
               :};
               
tipo_dato ::= INTEGER:INTEGER 
               {:   
                         colaTipoDato.add(INTEGER);
                         System.out.println("tipo_dato -> INTEGER");
               :}
               | FLOAT:FLOAT
               {:
                         colaTipoDato.add(FLOAT);
                         System.out.println("tipo_dato -> FLOAT");:}
               | STRING:STRING
               {:
                         colaTipoDato.add(STRING);
                         System.out.println("tipo_dato -> STRING");
               :};


factor         ::= ID:ID
               {: 
                    insertarEnPolaca(ID);
                    System.out.println("factor -> ID: " + ID);
                    System.out.println("ID: " + ID);
               :}
               | CONST_INT:CONST_INT 
               {:
                    insertarEnPolaca(CONST_INT);
                    tablaDeSimbolos.agregarEnTabla("_"+CONST_INT, null, CONST_INT, null);
                    System.out.println("factor -> CONST_INT: " + CONST_INT);
		          System.out.println("constante entera: " + CONST_INT);
		     :}
               | CONST_FLOAT:CONST_FLOAT
               {:   
                    insertarEnPolaca(CONST_FLOAT);
                    tablaDeSimbolos.agregarEnTabla("_"+CONST_FLOAT, null, CONST_FLOAT, null);
                    System.out.println("factor -> CONST_FLOAT: " + CONST_FLOAT);
		          System.out.println("constante flotante: " + CONST_FLOAT);
		     :}
               | CONST_STR:CONST_STR
               {:  
                    insertarEnPolaca(CONST_STR);
                    String stringSinComillas = CONST_STR.substring(0, CONST_STR.length() - 1);
                    tablaDeSimbolos.agregarEnTabla("_"+stringSinComillas, null, stringSinComillas, stringSinComillas.length());
                    System.out.println("factor -> CONST_STR: " + CONST_STR);
		          System.out.println("constante CONST_STR: " + CONST_STR);
		     :}
               | CTE_BIN:CTE_BIN
               {:
                    String valorBin = CTE_BIN.substring(2);
                    String valorDecimal = String.valueOf(Integer.parseInt(valorBin,2));
                    insertarEnPolaca(valorDecimal);
                    tablaDeSimbolos.agregarEnTabla("_"+CTE_BIN, null, valorBin, null);
                    System.out.println("factor -> cte_bin: " + CTE_BIN);
		          System.out.println("constante CTE_BIN: " + CTE_BIN);
		     :}
               | CTE_HEXA:CTE_HEXA
               {: 
                    String valorHexa = CTE_HEXA.substring(2);
                    String valorDecimal = String.valueOf(Integer.parseInt(valorHexa,16));
                    insertarEnPolaca(valorDecimal);
                    tablaDeSimbolos.agregarEnTabla("_"+CTE_HEXA, null, valorDecimal, null);
                    System.out.println("factor -> CTE_HEXA: " + CTE_HEXA);
		          System.out.println("constante CTE_HEXA: " + CTE_HEXA);
		     :}
               | PA expresion PC
               {:
                    System.out.println("factor -> PA expresion PC");
		     :}
               | CONTAR PA expresion PYC {:
                   insertarEnPolaca("@constanteAContar");
                   insertarEnPolaca(":");
                   insertarEnPolaca("0");
                   insertarEnPolaca("@contador");
                   insertarEnPolaca(":");
                   RESULT = new Symbol(-1); 

               :}  CA list_constantes CC PC {:
                 System.out.println("factor -> CONTAR PA expresion PYC CA list_exp CC PC");
                     insertarEnPolaca("@contador");
                    RESULT = new Symbol(-1); 
               :};

 list_constantes  ::= list_constantes COMA constantes
               {:  
                 insertarEnPolaca("@constanteAContar");
                   insertarEnPolaca("CMP");
                    insertarEnPolaca("BNE");
                    pila.push(polaca.size());
                    avanzar();
                    insertarEnPolaca("1");
                    insertarEnPolaca("@contador");
                    insertarEnPolaca("+");
                    insertarEnPolaca("@contador");
                    insertarEnPolaca(":");
                    insertarEnPolaca(String.valueOf(polaca.size()), pila.pop());
                    System.out.println("list_constantes -> list_constantes COMA constantes");
                     System.out.println(polaca);
                          System.out.println(polaca.size());

                    RESULT = new Symbol(-1); 
   
               :}
                | constantes
               {: 
                   insertarEnPolaca("@constanteAContar");
                   insertarEnPolaca("CMP");
                    insertarEnPolaca("BNE");
                    pila.push(polaca.size());
                    avanzar();
                    insertarEnPolaca("1");
                    insertarEnPolaca("@contador");
                    insertarEnPolaca("+");
                    insertarEnPolaca("@contador");
                    insertarEnPolaca(":");
                    insertarEnPolaca(String.valueOf(polaca.size()), pila.pop());
                    System.out.println("list_constantes -> constantes");
                     System.out.println(polaca);

                     RESULT = new Symbol(-1); 
               :} ;
constantes     ::= CONST_INT:CONST_INT {:
                          insertarEnPolaca(CONST_INT);
                          System.out.println("Constante: "+ CONST_INT);
                    :} | CONST_FLOAT:CONST_FLOAT {:
                          insertarEnPolaca(CONST_FLOAT);
                            System.out.println("Constante: "+ CONST_FLOAT);

                    :} | CTE_BIN:CTE_BIN {:
                          insertarEnPolaca(CTE_BIN);
                          System.out.println("Constante: "+ CTE_BIN);

                    :} | CTE_HEXA:CTE_HEXA {:
                          insertarEnPolaca(CTE_HEXA);
                         System.out.println("Constante: "+ CTE_HEXA);

                    :};